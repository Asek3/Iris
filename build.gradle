plugins {
	id 'dev.architectury.loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
}

loom {
    silentMojangMappingsLicense()
	accessWidenerPath = file("src/main/resources/iris.accesswidener")
	
	forge {
		convertAccessWideners = true
		mixinConfigs = [
				"mixins.oculus.json",
				"mixins.oculus.fantastic.json",
				"mixins.oculus.vertexformat.json",
				"mixins.oculus.bettermipmaps.json",
				"mixins.oculus.fixes.maxfpscrash.json",
				"oculus-batched-entity-rendering.mixins.json",
				"mixins.oculus.compat.sodium.json",
				"mixins.oculus.compat.indigo.json",
				"mixins.oculus.compat.indium.json"
		]
	}
	mixin.defaultRefmapName = "oculus-mixins-refmap.json"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

sourceSets {
	vendored {
		java {}
	}

	sodiumCompatibility {
		java {
			compileClasspath += main.compileClasspath
		}
	}

	main {
		java {
			compileClasspath += vendored.output
			runtimeClasspath += vendored.output

			compileClasspath += sodiumCompatibility.output
			runtimeClasspath += sodiumCompatibility.output
		}
	}
}


repositories {
	maven {
		url "https://maven.blamejared.com/"
	}
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	modCompileOnly "org.embeddedt:embeddium-${minecraft_version}:${embeddium_version}"

	implementation include("io.github.douira:glsl-transformer:2.0.0")
	implementation include("org.antlr:antlr4-runtime:4.11.1")
	implementation include("org.anarres:jcpp:1.4.14")
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

java {
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
